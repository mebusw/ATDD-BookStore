*** Settings ***
Test Setup        Common Setup
Test Teardown     Close Browser
Library           Selenium2Library

*** Variables ***
${username}       uperform
${password}       uperform
@{os}             windows    macos    linux
${browser}        chrome    #firefox
${title}          Refactoring: Improving the Design of Existing Code
${baseUrl}        http://127.0.0.1:8000/book

*** Test Cases ***
Built-in Syntax
    [Setup]
    Log Many    @{os}
    For Loop
    ${fullName}    Concat Full Name    Jacky    Shen
    Log    ${fullName}
    [Teardown]

Admin Can Add Then Delete Book
    Login To Admin    uperform    uperform
    Add A Book Thru Admin    Star Wars: Vader's Little Princess
    Page Should Contain    Star Wars: Vader's Little Princess
    Open Book By Name    Star Wars: Vader's Little Princess
    Delete Book Thru Admin
    Logout From Admin

Login With Wrong Username
    Page Should Contain    Book Store
    Login To Public    abcdefg    uperform
    Wait Until Page Contains    Wrong username or password!
    Page Should Contain    Wrong username or password!
    Page Should Contain Element    id=btnLogin

Login With Wrong Password
    Page Should Contain    Book Store
    Login To Public    uperform    1234
    Wait Until Page Contains    Wrong username or password!
    Page Should Contain    Wrong username or password!
    Page Should Contain Element    id=btnLogin

Login With Correct Username And Password
    Page Should Contain    Book Store
    Login To Public    uperform    uperform
    Wait Until Page Contains    uperform, Thanks for logging in
    Page Should Contain    uperform, Thanks for logging in
    Page Should Contain Element    id=btnLogout
    Logout From Public

Search For A Word That Is Likely A Title But Not Author
    Search With Word    navigation
    Search Results Should Include    Night Navigation    9780547335971
    Search Results Should Include    POINT TO POINT NAVIGATION    9780307275011

Search For A Word That Is Likely An Author But Not Title
    Search With Word    Fowler
    Search Results Should Include    Patterns of Enterprise Application Architecture    9780321127426
    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677

Search For A Word That Is Likely An Author And A Title
    Search With Word    brown
    Search Results Should Include    Inferno: A Novel (Robert Langdon)    9780385537858
    Search Results Should Include    Brown Bear, Brown Bear, What Do You See? My First Reader    9780805092448

Search For ISBN Number
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780385537858
    Search Results Should Include    Inferno: A Novel (Robert Langdon)    9780385537858
    Verify Book Detail    Inferno: A Novel (Robert Langdon)    9780385537858    Out-of-Stock

Pick An Out-of-stock Book Into The Cart
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780385537858
    ${link}    Search Results Should Include    Inferno: A Novel (Robert Langdon)    9780385537858
    Verify Book Detail    Inferno: A Novel (Robert Langdon)    9780385537858    Out-of-Stock
    Add Book to Cart    ${link}
    Wait Until Page Contains    The book will be shipped when it becomes available.
    Page Should Contain    The book will be shipped when it becomes available.

Pick An In-stock Book Into The Cart
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780201485677
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Verify Book Detail    Refactoring: Improving the Design of Existing Code    9780201485677    In-Stock
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    1

Pick A Second Book Into The Cart
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780201485677
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Verify Book Detail    Refactoring: Improving the Design of Existing Code    9780201485677    In-Stock
    Add Book to Cart    ${link}
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    2

Adjust The Quantity Of A Book From One To Ten
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780201485677
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Verify Book Detail    Refactoring: Improving the Design of Existing Code    9780201485677    In-Stock
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    1
    Adjust Quantity    10
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    10

Adjust The Quantity Of A Book To Zero
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780201485677
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Verify Book Detail    Refactoring: Improving the Design of Existing Code    9780201485677    In-Stock
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    1
    Adjust Quantity    0
    Cart Should Not Contain Book    Refactoring: Improving the Design of Existing Code

Not Logged-in User Can Not Checkout Cart
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780201485677
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    1
    Checkout Cart

Logged-in User Can Checkout Cart
    [Documentation]    Precondition: refered test data are already in database.
    Login To Public    uperform    uperform
    Wait Until Page Contains    uperform, Thanks for logging in
    Search With Word    9780201485677
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    1
    Checkout Cart
    Confirm Billing
    Wait Until Page Contains    Success.
    Page Should Contain    Success.
    Logout From Public

Billing Info Confirmed Should Be Updated To Profile
    [Documentation]    Precondition: refered test data are already in database.
    Login To Public    uperform    uperform
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    1
    Checkout Cart
    Fill Billing Info    Sanlitun, Beijing    No.1, Pudong, Shanghai    12345678
    Confirm Billing
    Verify Billing Info    Sanlitun, Beijing    No.1, Pudong, Shanghai    12345678
    Logout From Public

User Can Update Billing Info In Profile
    [Documentation]    Precondition: refered test data are already in database.
    Login To Public    uperform    uperform
    Update Billing Info    Wudaokou Beijing    No.9, Zhangjiang Shanghai    98765432
    Verify Billing Info    Wudaokou Beijing    No.9, Zhangjiang Shanghai    98765432
    Logout From Public

User Can View Own Bill History
    [Documentation]    Precondition: refered test data are already in database.
    Login To Public    uperform    uperform
    Wait Until Page Contains    uperform, Thanks for logging in
    Search With Word    9780201485677
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    1
    Checkout Cart
    Confirm Billing
    Wait Until Page Contains    Success.
    Page Should Contain    Success.
    ${billId}    Get Text    id=billId
    History Should Include    ${billId}
    Logout From Public

*** Keywords ***
For Loop
    @{items}    Evaluate    range(5)
    : FOR    ${var}    IN    @{items}
    \    Log    Hello, ${var}

Concat Full Name
    [Arguments]    ${firstName}    ${lastName}
    [Return]    ${firstName}, \ ${lastName}

Common Setup
    Open Browser    http://127.0.0.1:8000/book/    ${browser}
    Set Window Size    1000    600

Login To Public
    [Arguments]    ${username}    ${password}
    Input Text    id=username    ${username}
    Input Text    id=password    ${password}
    Click Button    id=btnLogin

Logout From Public
    Go To    ${baseUrl}
    Click Element    id=btnLogout

Search With Word
    [Arguments]    ${q}
    Go To    ${baseUrl}/search
    Input Text    id=q    ${q}
    Click Button    id=search

Search Results Should Include
    [Arguments]    ${title}    ${isbn}
    Page Should Contain Element    id=isbn_${ISBN}
    Page Should Contain    ${title}
    ${link}    Get Element Attribute    id=isbn_${ISBN}@href
    [Return]    ${link}

Verify Book Detail
    [Arguments]    ${title}    ${ISBN}    ${stock_state}
    Click Link    id=isbn_${ISBN}
    Page Should Contain    ${title}
    Page Should Contain    ${ISBN}
    Page Should Contain    ${stock_state}

Add Book to Cart
    [Arguments]    ${link}
    Go To    ${link}
    Wait Until Page Contains Element    id=btnAddToCart
    Click Element    id=btnAddToCart
    Sleep    1s

Verify Quantity In Cart
    [Arguments]    ${title}    ${qty}
    Go To    ${baseUrl}/cart/
    Wait Until Page Contains    ${title}
    Page Should Contain    ${title}
    ${fetched_qty}    Get Value    id=qty
    Should Be Equal    ${fetched_qty}    ${qty}

Adjust Quantity
    [Arguments]    ${qty}
    Go To    ${baseUrl}/cart/
    Input Text    id=qty    ${qty}
    Click Element    id=adjust
    Sleep    1s

Cart Should Not Contain Book
    [Arguments]    ${title}
    Go To    ${baseUrl}/cart/
    Page Should Not Contain    ${title}

Checkout Cart
    Click Element    id=checkout

Confirm Billing
    Wait Until Page Contains Element    id=btnConfirm
    Click Element    id=btnConfirm
    Sleep    1s

Fill Billing Info
    [Arguments]    ${billingAddr}    ${shippingAddr}    ${creditCard}
    Input Text    id=billingAddr    ${billingAddr}
    Input Text    id=shippingAddr    ${shippingAddr}
    Input Text    id=creditCard    ${creditCard}

Verify Billing Info
    [Arguments]    ${billingAddr}    ${shippingAddr}    ${creditCard}
    Go To    ${baseUrl}/profile
    ${fetched_billingAddr}    Get Value    id=billingAddr
    ${fetched_shippingAddr}    Get Value    id=shippingAddr
    ${fetched_creditCard}    Get Value    id=creditCard
    Should Be Equal    ${fetched_billingAddr}    ${billingAddr}
    Should Be Equal    ${fetched_shippingAddr}    ${shippingAddr}
    Should Be Equal    ${fetched_creditCard}    ${creditCard}
    Click Element    id=btnCancel

Update Billing Info
    [Arguments]    ${billingAddr}    ${shippingAddr}    ${creditCard}
    Wait Until Page Contains Element    id=btnProfile
    Click Element    id=btnProfile
    Wait Until Page Contains    User Profile
    Fill Billing Info    ${billingAddr}    ${shippingAddr}    ${creditCard}
    Wait Until Page Contains Element    id=btnUpdate
    Click Element    id=btnUpdate
    Sleep    1

History Should Include
    [Arguments]    ${billId}
    Go To    ${baseUrl}/bills
    Page Should Contain Element    id=billId_${billId}

Login To Admin
    [Arguments]    ${username}    ${password}
    Go To    http://127.0.0.1:8000/admin/
    Input Text    id=id_username    ${username}
    Input Text    id=id_password    ${password}
    Click Button    css=input[type='submit']
    Page Should Contain    ${username}

Logout From Admin
    Go To    http://127.0.0.1:8000/admin/logout

Add A Book Thru Admin
    [Arguments]    ${bookName}
    Click Link    link=Books
    Wait Until Page Contains Element    link=Add book
    Click Link    link=Add book
    Wait Until Page Contains Element    id=id_title
    Input Text    id=id_title    ${bookName}
    Input Text    id=id_pub_date_0    2013-04-22
    Input Text    id=id_pub_date_1    22:06:30
    Input Text    id=id_quatity    20
    Select Checkbox    id=id_in_stock
    Input Text    id=id_isbn    9781452118697
    Input Text    id=id_rating    5
    Input Text    id=id_description    Das Vader
    Input Text    id=id_numberOfPages    99
    Input Text    id=id_price    6
    Select From List    id_authors    Bill    Jobs
    Click Button    css=input[value='Save']

Delete Book Thru Admin
    Wait Until Page Contains Element    link=Delete
    Click Link    link=Delete
    Wait Until Page Contains Element    css=input[type='submit']
    Click Button    css=input[type='submit']
    Page Should Contain    was deleted successfully.

Open Book By Name
    [Arguments]    ${bookName}
    Click Link    ${bookName}
