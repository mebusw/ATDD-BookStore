*** Settings ***
Test Setup        Open Browser    http://127.0.0.1:8000/book/    ${browser}
Test Teardown     Close Browser
Library           Selenium2Library

*** Variables ***
${username}       uperform
${password}       uperform
@{os}             windows    macos    linux
${browser}        chrome
${title}          Refactoring: Improving the Design of Existing Code

*** Test Cases ***
Built-in Syntax
    [Setup]
    Log Many    @{os}
    For Loop
    ${fullName}    Full Name    Jacky    Shen
    Log    ${fullName}
    [Teardown]

Admin Site
    Go To    http://127.0.0.1:8000/admin/
    Page Should Contain    admin

Login With Wrong Username
    Page Should Contain    Welcome to Book Store.
    Login To Public    abcdefg    uperform
    Page Should Contain    Wrong username or password!
    Page Should Contain Element    id=btnLogin

Login With Wrong Password
    Page Should Contain    Welcome to Book Store.
    Login To Public    uperform    1234
    Page Should Contain    Wrong username or password!
    Page Should Contain Element    id=btnLogin

Login With Correct Username And Password
    Page Should Contain    Welcome to Book Store.
    Login To Public    uperform    uperform
    Page Should Contain    Welcome, uperform. Thanks for logging in
    Page Should Contain Element    id=btnLogout
    Logout From Public

Search For A Word That Is Likely A Title But Not Author
    Search With Word    navigation
    Search Results Should Include    Night Navigation    9780547335971
    Search Results Should Include    POINT TO POINT NAVIGATION    9780307275011

Search For A Word That Is Likely An Author But Not Title
    Search With Word    Fowler
    Search Results Should Include    Patterns of Enterprise Application Architecture    9780321127426
    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677

Search For A Word That Is Likely An Author And A Title
    Search With Word    brown
    Search Results Should Include    Inferno: A Novel (Robert Langdon)    9780385537858
    Search Results Should Include    Brown Bear, Brown Bear, What Do You See? My First Reader    9780805092448

Search For ISBN Number
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780385537858
    Search Results Should Include    Inferno: A Novel (Robert Langdon)    9780385537858
    Verify Book Detail    Inferno: A Novel (Robert Langdon)    9780385537858    Out-of-Stock

Pick An Out-of-stock Book Into The Cart
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780385537858
    ${link}    Search Results Should Include    Inferno: A Novel (Robert Langdon)    9780385537858
    Verify Book Detail    Inferno: A Novel (Robert Langdon)    9780385537858    Out-of-Stock
    Add Book to Cart    ${link}
    Page Should Contain    The book will be shipped when it becomes available.

Pick An In-stock Book Into The Cart
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780201485677
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Verify Book Detail    Refactoring: Improving the Design of Existing Code    9780201485677    In-Stock
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    1

Pick A Second Book Into The Cart
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780201485677
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Verify Book Detail    Refactoring: Improving the Design of Existing Code    9780201485677    In-Stock
    Add Book to Cart    ${link}
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    2

Adjust The Quantity Of A Book From One To Ten
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780201485677
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Verify Book Detail    Refactoring: Improving the Design of Existing Code    9780201485677    In-Stock
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    1
    Adjust Quantity    10
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    10

Adjust The Quantity Of A Book To Zero
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780201485677
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Verify Book Detail    Refactoring: Improving the Design of Existing Code    9780201485677    In-Stock
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    1
    Adjust Quantity    0
    Cart Should Not Contain Book    Refactoring: Improving the Design of Existing Code

Not Logged-in User Can Not Checkout Cart
    [Documentation]    Precondition: refered test data are already in database.
    Search With Word    9780201485677
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    1
    Checkout Cart
    Page Should Contain Element    id=btnLogin

Logged-in User Can Checkout Cart
    [Documentation]    Precondition: refered test data are already in database.
    Login To Public    uperform    uperform
    Search With Word    9780201485677
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    1
    Checkout Cart
    Fill Billing Info    Sanlitun, Beijing    No.1, Pudong, Shanghai    12345678
    Confirm Billing
    Page Should Contain    Welcome, uperform. Thanks for logging in.
    Logout From Public

Billing Info Confirmed Should Be Updated To Profile
    [Documentation]    Precondition: refered test data are already in database.
    Login To Public    uperform    uperform
    ${link}    Search Results Should Include    Refactoring: Improving the Design of Existing Code    9780201485677
    Add Book to Cart    ${link}
    Verify Quantity In Cart    Refactoring: Improving the Design of Existing Code    1
    Checkout Cart
    Fill Billing Info    Sanlitun, Beijing    No.1, Pudong, Shanghai    12345678
    Confirm Billing
    Verify Billing Info    Sanlitun, Beijing    No.1, Pudong, Shanghai    12345678
    Logout From Public

User Can Update Billing Info In Profile
    [Documentation]    Precondition: refered test data are already in database.
    Login To Public    uperform    uperform
    Update Billing Info    Wudaokou Beijing    No.9, Zhangjiang Shanghai    98765432
    Verify Billing Info    Wudaokou Beijing    No.9, Zhangjiang Shanghai    98765432
    Logout From Public

*** Keywords ***
For Loop
    @{items}    Evaluate    range(5)
    : FOR    ${var}    IN    @{items}
    \    Log    Hello, ${var}

Full Name
    [Arguments]    ${firstName}    ${lastName}
    [Return]    ${firstName}, \ ${lastName}

Login To Public
    [Arguments]    ${username}    ${password}
    Input Text    id=username    ${username}
    Input Text    id=password    ${password}
    Click Button    id=btnLogin

Logout From Public
    Click Element    id=btnLogout

Search With Word
    [Arguments]    ${q}
    Go To    http://127.0.0.1:8000/book/search
    Input Text    id=q    ${q}
    Click Button    id=search

Search Results Should Include
    [Arguments]    ${title}    ${isbn}
    Page Should Contain Element    id=isbn_${ISBN}
    Page Should Contain    ${title}
    ${link}    Get Element Attribute    id=isbn_${ISBN}@href
    [Return]    ${link}

Verify Book Detail
    [Arguments]    ${title}    ${ISBN}    ${stock_state}
    Click Link    id=isbn_${ISBN}
    Page Should Contain    ${title}
    Page Should Contain    ${ISBN}
    Page Should Contain    ${stock_state}

Add Book to Cart
    [Arguments]    ${link}
    Go To    ${link}
    Wait Until Page Contains Element    id=btnAddToCart
    Click Element    id=btnAddToCart

Verify Quantity In Cart
    [Arguments]    ${title}    ${qty}
    Go To    http://127.0.0.1:8000/book/cart/
    Page Should Contain    ${title}
    ${fetched_qty}    Get Value    id=qty
    Should Be Equal    ${fetched_qty}    ${qty}

Adjust Quantity
    [Arguments]    ${qty}
    Go To    http://127.0.0.1:8000/book/cart/
    Input Text    id=qty    ${qty}
    Click Element    id=adjust

Cart Should Not Contain Book
    [Arguments]    ${title}
    Go To    http://127.0.0.1:8000/book/cart/
    Page Should Not Contain    ${title}

Checkout Cart
    Click Element    id=checkout

Confirm Billing
    Click Element    id=btnConfirm

Fill Billing Info
    [Arguments]    ${billingAddr}    ${shippingAddr}    ${creditCard}
    Input Text    id=billingAddr    ${billingAddr}
    Input Text    id=shippingAddr    ${shippingAddr}
    Input Text    id=creditCard    ${creditCard}

Verify Billing Info
    [Arguments]    ${billingAddr}    ${shippingAddr}    ${creditCard}
    Click Element    id=btnProfile
    ${fetched_billingAddr}    Get Value    id=billingAddr
    ${fetched_shippingAddr}    Get Value    id=shippingAddr
    ${fetched_creditCard}    Get Value    id=creditCard
    Should Be Equal    ${fetched_billingAddr}    ${billingAddr}
    Should Be Equal    ${fetched_shippingAddr}    ${shippingAddr}
    Should Be Equal    ${fetched_creditCard}    ${creditCard}
    Click Element    id=btnCancel

Update Billing Info
    [Arguments]    ${billingAddr}    ${shippingAddr}    ${creditCard}
    Click Element    id=btnProfile
    Fill Billing Info    ${billingAddr}    ${shippingAddr}    ${creditCard}
    Click Element    id=btnUpdate
